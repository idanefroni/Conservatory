#!/usr/bin/perl
use POSIX;
use strict;
use Getopt::Long;
use Cwd 'abs_path';
use List::MoreUtils qw(firstidx);

###########

my $conservatoryDir=abs_path(".");
my $genome="";
my $family;
my $force=0;
my $verbose=0;
my $help=0;
my $genomeWasProcessed=0;
my $threads=16;
my @threadPids;
my $version=2;

my $skipAlignment=0;
my $skipConservation=0;
my $minIdentity=0;
my $minSpeciesNum= -1;
my $minPhyloPscore=0;
my $referenceGenome="";
my $geneListFileName;
my $global=1;

GetOptions ("conservatoryDirectory=s" => \$conservatoryDir,
			"reference=s" => \$referenceGenome,
			"threads=i" => \$threads,
			"version=i" => \$version,
			"genelist=s" => \$geneListFileName,
			"min-identity=i" => \$minIdentity,
			"min-species=i" => \$minSpeciesNum,
			"min-phylop-score=f" => \$minPhyloPscore,
			"skip-alignment" => \$skipAlignment,
			"skip-conservation" => \$skipConservation,
			"force" => \$force,
			"verbose" => \$verbose,
			"help" => \$help) or die("Error in command line arguments\n");

			
if($help || (!$global && $family eq "") || ($version != 1 && $version !=2) || ($global && $referenceGenome eq "")) {
	print "Conservatory version 0.0.1\n\n";
	
	print "processConservation --family <family>\n\n";
	print "\t--conservatoryDirectory\tPath of the main conservatory directory. See README for directory structure. (DEFAULT: current directory)\n";
	print "\t--reference\t\tThe reference genome (REQUIRED).\n";
	print "\t--genelist\t\tThe list of genes to process (DEFAULT: all genes in the reference genome).\n";
	print "\t--min-identity\t\tMinimum identity for LASTZ alignments.\n";
	print "\t--min-species\t\tMinimum number of species share sequence for an alignment to be considered for CNS.\n";
	print "\t--min-phylop-score\t\tMinimum phyloP score to consider as significant CNS.\n";
	print "\t--version\t\tVersion of aligner to use (1 or 2; DEFAULT 2)\n";	
	print "\t--skip-alignmnet\t\tSkip the alignment step.\n";
	print "\t--skip-conservation\t\tSkip phyloP conservation calculation step.\n";
	print "\t--force\t\tForce rebuilding alignments.\n";
	print "\t--threads\t\tNumber of threads to use.\n";
	print "\t--verbose\t\tOutput extra progress messages.\n";
	print "\t--help\t\t\tPrints this message.\n\n";
	exit(0);
}

#################### Set up files
my $genomedb_file = "$conservatoryDir/genome_database.csv";
my $outputDir = "$conservatoryDir/alignments/$family";
if($global) {
	$outputDir = "$conservatoryDir/alignments/$referenceGenome";
}
my $conservationDir = "$conservatoryDir/CNS";
my $alignmentDir = "$conservatoryDir/alignments";
my $genomesDir = "$conservatoryDir/genomes";


#### set up parallel processing
@threadPids = (0) x $threads;

####### First, sanity checks. Check to see if blast is installed and if directory structure is OK.
####### 
die "ERROR: Cannot find file genome database file ($genomedb_file)\n" unless -e $genomedb_file;
die "ERROR: Conservatory directory structure at ($conservatoryDir) is corrupt\n" unless (-e "$conservatoryDir/genomes" && -e "$conservatoryDir/genomes/blastdb/" && -e "$conservatoryDir/scripts" && -e "$conservatoryDir/alignments");

my $t_lastz = `sh -c 'command -v lastz'`;
die "ERROR: Cannot find lastz in path. Please make sure it is installed.\n" unless ($t_lastz ne ""); 

my $t_samtools = `sh -c 'command -v samtools'`;
die "ERROR: Cannot find samtools in path. Please make sure it is installed.\n" unless ($t_samtools ne "");  

my $t_phyloP = `sh -c 'command -v phyloP'`;
die "ERROR: Cannot find phyloP in path. Please make sure phast is installed.\n" unless ($t_phyloP ne "");  

######## Read genome database file.
	
open(my $genomedb, "<", $genomedb_file);
my $header=<$genomedb>;
while(my $curgenomeline= <$genomedb>) {
	my ($curgenomeName, $curgenomeSpecies,  $curgenomeFamily, $curgenomeReference) = split /,/, $curgenomeline;

	if( (!$global && ($family eq $curgenomeFamily && $curgenomeName eq $curgenomeReference)) || ($global && $curgenomeName eq $referenceGenome) ) {
		$genomeWasProcessed=1;
		
		#### First, genome sanity check. See that we have all files.
		if($global) {
			print localtime() . ": START processing conservation for  reference genome $referenceGenome\n";
		} else {
			print localtime() . ": START processing conservation for $family (reference genome $curgenomeName)\n";
		}
		if($geneListFileName eq "") {
			$geneListFileName = "$conservatoryDir/genomes/$curgenomeFamily/$curgenomeName.footprint.gff3";
		}
		die localtime() . ": ABORT Cannot find gene list file name ($geneListFileName).\n" unless -e $geneListFileName;
		
		### Start threads
		if(!($skipAlignment && $skipConservation)) {
			
			open(my $geneListFile, "<", $geneListFileName);
			while(my $geneline = <$geneListFile>) {
					chomp($geneline);
					my $curLocus;
					my @array = split /\t/, $geneline;
					my %geneparam = split/[;=]/, $array[8];
					($curLocus) = $geneparam{'Name'};
					
					## Get genename
					$curLocus =~ s/^.[^-]*-[^-]*-//;
					
					# now find a place for the thread
					
					my $placeForProcess = firstidx { $_ == 0 } @threadPids;
					### If the queue is full, wait for the next spot 
					if( $placeForProcess == -1 ) {
						my $freedPid = wait();
						$placeForProcess = firstidx { $_ == $freedPid } @threadPids;
					}
					#### now fork the alignment
					my $forkChildPid = fork();
					if(!$forkChildPid) {	#### In the child process
						print localtime() . ": Thread $placeForProcess is processing $curLocus.\n";
						my $alignmentParams;
						
						if ($global) {
							$alignmentParams = "--reference $referenceGenome --locus $curLocus ";
						} else {
							$alignmentParams = "--family $family --locus $curLocus ";
						}						
						if($minIdentity>0) { $alignmentParams .= " --min-identity $minIdentity"; }
						if($minSpeciesNum>=0) { $alignmentParams .= " --min-species $minSpeciesNum"; }
						if($minPhyloPscore>0) { $alignmentParams .= " --min-phylop-score $minPhyloPscore"; }   		
						if($force) { $alignmentParams .= " --force"; }
						if($skipAlignment) { $alignmentParams .= " --skip-alignment"; }
						if($skipConservation) { $alignmentParams .= " --skip-conservation"; }
						if($global) {
							system("perl $conservatoryDir/scripts/buildConservation $alignmentParams");
						} else {
							system("perl $conservatoryDir/scripts/buildConservationForFamily$version $alignmentParams");
						}
						exit;
					} else {
						$threadPids[$placeForProcess] = $forkChildPid;
					}
			}
			## wait for all child processes to finish
			for my $curPid (@threadPids) {
				if($curPid != 0) { waitpid($curPid, 0); }
			}
			close($geneListFile);
			print localtime() . ": END Finished processing all alignments.\n";
		}
		## Merge all BAM, sort and index
		print localtime() . ": START data merge and sort.\n";
		if($global) {
			$family = $referenceGenome;
			$curgenomeReference = $referenceGenome;
		}
		unlink("$alignmentDir/$family.bam");
		system("find $outputDir -name \"*.bam\" > $outputDir/$family.bams.txt");
		system("ulimit -n 200000; samtools merge -b  $outputDir/$family.bams.txt $alignmentDir/$family.tmp.bam");
		unlink("$outputDir/$family.bams.txt");
		
		system("samtools sort $alignmentDir/$family.tmp.bam > $alignmentDir/$family.sort.bam");
		unlink("$alignmentDir/$family.tmp.bam");

		# Mark snps and indels
		system("samtools calmd -@ $threads -b $alignmentDir/$family.sort.bam $conservatoryDir/genomes/$curgenomeFamily/$curgenomeReference.fasta.gz > $alignmentDir/$family.bam");
		unlink("$alignmentDir/$family.sort.bam");
		system("samtools index $alignmentDir/$family.bam");
			
		##### Now merge conservation data
		unlink("$conservationDir/$family.csv");
		unlink("$conservationDir/$family.bed");
		unlink("$conservationDir/$family.bw");
		
		# merge gene files for entire genome
		system("for a in $conservationDir/$family/*.csv; do cat \$a >> $conservationDir/$family.csv.tmp; done");
		system("for a in $conservationDir/$family/*.bed; do cat \$a | sed 's/ /\t/g' >> $conservationDir/$family.bed; done");
		system("for a in $alignmentDir/$family/*.map.txt; do cat \$a >> $alignmentDir/$family.map.txt.tmp; done");
	
		# sort files
		system("sort -t',' -k1,1 -k7,7n $conservationDir/$family.csv.tmp | uniq > $conservationDir/$family.csv");
		system("sort -k1,1 -k2,2n -u -o $conservationDir/$family.bed $conservationDir/$family.bed");
		system("sort -k1,1 -k2,2nr $alignmentDir/$family.map.txt.tmp | uniq > $alignmentDir/$family.map.txt");
		unlink("$conservationDir/$family.csv.tmp");
		unlink("$alignmentDir/$family.map.txt.tmp");

		# Make nice output files. BW for the phylop track and GFF for the CNS
		system("bedGraphToBigWig $conservationDir/$family.bed $genomesDir/$curgenomeFamily/$curgenomeReference.chrom.size $conservationDir/$family.bw");
		system("perl $conservatoryDir/scripts/buildConCNS --family  $family --format GFF --CNSfile $conservationDir/$family.csv > $conservationDir/$family.gff3");
		print localtime() . ": END data merge and sort.\n";		
		## Process CNS tables and flag TEs
		print localtime() . ": START build CNS tables.\n";
		system("perl $conservatoryDir/scripts/buildCNSTables $family $conservationDir/$family.csv $alignmentDir/$family.map.txt $conservatoryDir/genomes/TE/$family.te.gff3");

		print localtime() . ": END CNS table build.\n";
		
		print localtime() . ": END processing conservation for $family (reference genome $curgenomeName)\n";
	}
}
close($genomedb);
                                                                           
if(! $genomeWasProcessed) { die ("ERROR: No family $family or no reference genome was found in $genomedb_file.\n"); }

print localtime() . ": END conservation processing run.\n";



