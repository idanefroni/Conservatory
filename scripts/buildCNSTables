#!/usr/bin/perl

use POSIX;
use strict;
use Cwd 'abs_path';
use List::Util qw(min max);
use Statistics::Descriptive;
use Bio::AlignIO;
use Bio::Tools::GFF;
use Bio::SeqFeatureI;

print STDERR "\nUsage: buildCNSTables referenceGenome cnsFile mapFile teGFFfile\n\nAccepts CNS and alignment map files and output generate two files:\n\n";
print STDERR "\t\t[family].cns.table.csv\t\tFiltered CNS file\n\t\t[family].cns.position.table.csv\t\tThe position of the CNS in each of the family genomes\n";
print STDERR "\n\nThe filtered CNS table has the following fields:\n";
print STDERR "\tCNS_Unique_Name_In_Reference, ReferenceGenome, Chromosome, Start, End, ORFCheck, TECheck\n\n";
print STDERR "The position table has the following fields:\n";
print STDERR "\tCNS_Unique_Name, ReferenceGene, Genome, Gene, RelativeStart, UporDown, phyloP-pvalue\n\n\n";

if(scalar @ARGV< 3) {
	die "\n\nERROR: No CNS and map file provided.\n";
}

open (my $CNSInputFile,"sort -t',' -k6,6 -k7,7n $ARGV[1] |") ||  die "ERROR: Cannot find CNS file $ARGV[1].\n";


################### Identify the reference genome
my $referenceGenome=$ARGV[0];
my $referenceGenomeFamily;
my $referenceSpecies;

my $conservatoryDir=abs_path(".");
my $genomedbFile = $conservatoryDir . "/genome_database.csv";
open (my $genomeDatabase, "<", $genomedbFile) || die "ERROR: Cannot open genome database $genomedbFile.\n";
my $header = <$genomeDatabase>;
while((my $curgenomeline = <$genomeDatabase>) && $referenceGenomeFamily eq "") {
	my ($curgenomeName, $curgenomeSpecies,  $curgenomeFamily, $curgenomeReference, $upstreamLength, $downstreamLength, $geneNameField, $geneProcessingRegEx, $gene2SpeciesIdentifier, $proteinProcessingRegEx) = split /,/, $curgenomeline;
	if($curgenomeName eq $referenceGenome) {
		$referenceGenomeFamily = $curgenomeFamily;
		$referenceSpecies = $curgenomeSpecies;
	}
}
die "ERROR: Cannot find reference genome $referenceGenome in genome_database.csv" unless $referenceGenomeFamily ne "";

my %CNSDB;
my $CNSCounter=1;
my @CNSList;
my %targetSpecies;
my %targetSpeciesPerLocus;

$| = 1;

######## Read the mapping file into memory
#### Now add the positions from the mapping file and index it
my %mappings;

print localtime() . ": Reading mapping file...";
open (my $mapInputFile, $ARGV[2]) ||  die "ERROR: Cannot find map file $ARGV[2].\n";
while(<$mapInputFile>) {
	chomp;
	my ($locus, $startRefRel, $length, $targetGenome,$targetLocus, $targetStartRel) = split /\t/;
	push (@ { $mappings{$locus} }, {
		'Locus' => $locus,
		'StartRefRel' => $startRefRel,
		'Length' => $length,
		'TargetGenome' => $targetGenome,
		'TargetLocus' => $targetLocus,
		'TargetStartRel' => $targetStartRel	
	});
	$targetSpecies{$targetGenome} = 1;
	$targetSpeciesPerLocus{$locus}{$targetGenome} = 1;

}

close($mapInputFile);
print "\n" . localtime() . ": Done.";

####### Load TE GFF
my $markTE=0;
my $gff;
my $gffFilename= $ARGV[3];
my $curTE;

if($gffFilename ne "") {
	if(-e $gffFilename) {
		$markTE=1;
	}
}

if($markTE) {
	print "\n" . localtime() . ": Opening TE GFF.";
	$gff = Bio::Tools::GFF->new(-file => $gffFilename,
				   -gff_version => 3);
	$curTE = $gff->next_feature;
}


## A CNS Object has the following fields: Name, Chromosome, Start, End, ORFCheck, TECheck, ConservationLevel, PositionVariability
##     

# First pass. Generate the CNS databse while merging overlapping CNS and marking TE
my $previousCNS = "";
print "\n" . localtime() . ": Collecting and merging CNS...";
while(<$CNSInputFile>) {
	my $overlapCNS = "";
	my $overlapTE;
	my $doneWithTE=0;
	my $startlog=0;
	
	chomp;
	my ($locus, $startRel, $endRel, $pval,$upordown, $chromosome, $start, $end, $orf) = split /,/;
	if($orf eq "ORF" || $orf eq "1") { $orf = "1"; } else { $orf = "0"; }
	### Check for overlaps 
	if($previousCNS ne "") {
		if($CNSDB{$previousCNS}{'Chromosome'} == $chromosome && overlap($start, $end, $CNSDB{$previousCNS}{'Start'},$CNSDB{$previousCNS}{'End'})) {
			$overlapCNS = $previousCNS;
		}
	}
	
	if($markTE) {
		### first, get to the right chromosome
		while(!$doneWithTE && ($chromosome ne $curTE->seq_id)) {
			my $tmpCurTE = $gff->next_feature;
			if($tmpCurTE) {
				$curTE=$tmpCurTE;
			} else {
				$doneWithTE=1;
			}

		}
		## now get to the right place
		while(!$doneWithTE && ($chromosome eq $curTE->seq_id && $curTE->end < $start )) {
			my $tmpCurTE = $gff->next_feature;
			if($tmpCurTE) {
				$curTE=$tmpCurTE;
			} else {
				$doneWithTE=1;
			}
		}

		## If the TE is in more than 20% of the CNS, mark the overlap
		if($curTE->seq_id eq $chromosome && overlap($start, $end, $curTE->start, $curTE->end)>0.2) {
			$overlapTE="1";
		} else {
			$overlapTE="0";
		}
		
	} else {
		$overlapTE="0";
	}
	
	if($overlapCNS eq "") {
		my $newCNSName = "CNS_" . $referenceGenome . "_$CNSCounter";
		$CNSDB{$newCNSName} = {
			'Name' => $newCNSName, 
			'Chromosome' => $chromosome,
			'Start' => $start,
			'End' => $end,
			'ORFCheck' => $orf,
			'TECheck' => $overlapTE,
			'Position' => ()};
		$previousCNS = $newCNSName;
		
		push @CNSList, $newCNSName;
			
		$CNSCounter++;
	} else {
		## Merge the CNS.
		$CNSDB{$overlapCNS}{'Start'} = min($start, $CNSDB{$overlapCNS}{'Start'});
		$CNSDB{$overlapCNS}{'End'} = max($end, $CNSDB{$overlapCNS}{'End'});
		if($overlapTE eq "1") {
			$CNSDB{$overlapCNS}{'TECheck'} = "1";
		}
		if($orf eq "1") {
			$CNSDB{$overlapCNS}{'ORFCheck'} = "1";
		}
		push @CNSList, $overlapCNS;
		
		$previousCNS = $overlapCNS;
	}
	if($CNSCounter % 10000 == 0 ) { print "."; }
}
print "\n" . localtime() . ": Collected " . scalar %CNSDB . " CNSs.\n";
close($CNSInputFile);

print localtime() . ": Collect positions of CNS from reference and target genomes...\n";

open ($CNSInputFile,"sort -t',' -k6,6 -k7,7n $ARGV[1] |") ||  die "ERROR: Cannot find CNS file $ARGV[1].\n";
my $CNSLineCounter =0;

while(<$CNSInputFile>) {
	chomp;
	my ($locus, $startRel, $endRel, $pval,$upordown, $chromosome, $start, $end, $orf) = split /,/;
	my $relatedCNSName = $CNSList[$CNSLineCounter];
	push (@{ $CNSDB{$relatedCNSName}{'Position'} }, {
		'Genome' => $referenceGenome,
		'RefLocus' => "$referenceSpecies-$referenceGenome-$locus",
		'TargetLocus' => "$referenceSpecies-$referenceGenome-$locus",
		'StartRel' => $startRel,
		'phyloPval' => $pval,
		'UpDown' => $upordown});
	
	my @targetPositions = @{ $mappings{$locus} };
	foreach my $curTargetPos (@{ targetPositions}) {
		if(overlap($startRel, $endRel,  $curTargetPos->{'StartRefRel'},  $curTargetPos->{'StartRefRel'} +  $curTargetPos->{'Length'})) {
			push (@{ $CNSDB{$relatedCNSName}{'Position'} }, {
				'Genome' => $curTargetPos->{'TargetGenome'},
				'RefLocus' => "$referenceSpecies-$referenceGenome-$locus",
				'TargetLocus' => $curTargetPos->{'TargetLocus'},
				'StartRel' => $curTargetPos->{'TargetStartRel'},
				'phyloPval' => $pval,
				'UpDown' => $upordown});
		}
	}
	
	$CNSLineCounter++;
}

print localtime() . ": Done collecting positions from reference.\n";

close ($CNSInputFile);
####### Done filtering and indexing
### Now calculate CNS level statistics


print localtime() . ": Done calculating statistics.\n";
######### Finally, dump the tables

open (my $outputCNS, ">$referenceGenome.cns.table.csv");
open (my $outputPosition, ">$referenceGenome.cns.position.table.csv");

foreach my $curCNS ( keys %CNSDB ) {
	print $outputCNS $curCNS . "," .
					 $referenceGenome . "," .
					 $CNSDB{$curCNS}{'Chromosome'} . "," . 
					 $CNSDB{$curCNS}{'Start'} . "," .
					 $CNSDB{$curCNS}{'End'} . "," .
					 $CNSDB{$curCNS}{'ORFCheck'} . "," .
					 $CNSDB{$curCNS}{'TECheck'} . "\n";
				 
				 
	my @positions = @{ $CNSDB{$curCNS}{'Position'} };
	
	foreach my $curPos ( @positions ) {
		print $outputPosition $curCNS . "," .
							  $curPos->{'RefLocus'} . "," . 
						      $curPos->{'Genome'} . "," .
						      $curPos->{'TargetLocus'} . "," .
						      $curPos->{'StartRel'} . "," .
						      $curPos->{'UpDown'} . "," . 
						      $curPos->{'phyloPval'} . "\n";	
	}
}

close($outputCNS);
close($outputPosition);


### Compute overlap between two fragments
sub overlap {
	my ($startOne, $endOne, $startTwo, $endTwo) =@_;
	if ( ($startOne < $startTwo && $endOne < $startTwo) ||
		 ($startOne > $endTwo)) {
		return 0;
	} else {
		#### return the percent overlap
		if($endOne < $endTwo) {
			if($startOne >= $startTwo) {
				return 1;
			} else {
				return ($endOne -$startTwo) /($endOne - $startOne);
			} 
		} else {
			if($startOne <= $startTwo) {
				return 1;
			} else {
				return ($endTwo -$startOne) /($endOne - $startOne);
			} 			
		}
	}
}
